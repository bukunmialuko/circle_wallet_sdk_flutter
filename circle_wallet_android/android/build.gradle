plugins {
    id "com.android.library"
    id "kotlin-android"
}

group 'com.example.verygoodcore'
version '1.0-SNAPSHOT'

allprojects {
    repositories {
        google()
        mavenCentral()

        def pwLocalPropertiesFile = new File(rootDir, "local.properties")

        if (pwLocalPropertiesFile.exists()) {
            Properties properties = new Properties()
            properties.load(pwLocalPropertiesFile.newDataInputStream())

            def mavenUrl = properties.getProperty('pwsdk.maven.url')
            def mavenUsername = properties.getProperty('pwsdk.maven.username')
            def mavenPassword = properties.getProperty('pwsdk.maven.password')

            if (mavenUrl != null && !mavenUrl.isEmpty()) {
                maven {
                    url mavenUrl
                    credentials {
                        username mavenUsername ?: ""
                        password mavenPassword ?: ""
                    }
                }
            }
        }
    }
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.example.verygoodcore'
    }

    compileSdk 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion 19
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}

dependencies {
    compileOnly files("$flutterRoot/bin/cache/artifacts/engine/android-arm/flutter.jar")

    implementation "circle.programmablewallet:sdk:1.0.1189"

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.mockito:mockito-core:5.0.0'
}