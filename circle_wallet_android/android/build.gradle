plugins {
    id "com.android.library"
    id "kotlin-android"
}

group 'com.example.verygoodcore'
version '1.0-SNAPSHOT'

allprojects {
    repositories {
        google()
        mavenCentral()
        // Circle Programmable Wallet SDK repository
        def localPropertiesFile = new File(rootProject.projectDir, "../../../android/local.properties")

        if (localPropertiesFile.exists()) {
            Properties properties = new Properties()
            properties.load(localPropertiesFile.newDataInputStream())

            def mavenUrl = properties.getProperty('pwsdk.maven.url')

            if (mavenUrl != null && !mavenUrl.isEmpty()) {
                maven {
                    url mavenUrl
                    credentials {
                        username properties.getProperty('pwsdk.maven.username') ?: ""
                        password properties.getProperty('pwsdk.maven.password') ?: ""
                    }
                }
            }
        }
    }
}

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.example.verygoodcore'
    }

    compileSdk 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion 19
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}

dependencies {
    implementation "circle.programmablewallet:sdk:1.0.1189"

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.mockito:mockito-core:5.0.0'
}